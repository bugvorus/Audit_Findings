### The absence of checks for signature deadlines in the `GuitarCryptone::importNfts` and `GuitarCryptone::_permit` functions could result in the signature remaining valid even after the deadline has passed.

**Description:** The functions `GuitarCryptone::importNfts` and `GuitarCryptone::_permit` have a parameter for specifying a deadline for transactions that require a signature. However, these functions don't have checks in place to ensure that the deadline has passed or not. This means that even if the deadline for the signature has expired, users can still perform transactions using these functions. 

Moreover, the `_permit` function is especially vulnerable because it's an external function without any access control. This means that a potentially malicious user could exploit this vulnerability to use the signature even after it's supposed to be invalid.

**Impact:** Signatures with a specified deadline, if not verified, can still be utilized even after the deadline has passed.

**Recommended Mitigation:** Make sure to always verify the deadline for signatures and ensure they're not accepted after they've expired. We suggest making the following adjustments to both functions.

In `GuitarCryptone::importNfts`

```diff
function importNfts(
    address from_,
    uint256 erc721Type_,
    uint256[] calldata tokenIds_,
    string calldata id_,
    uint256 deadline,
    bytes[] calldata signatures
  ) external onlyRole(OPERATOR_ROLE) {
+   require(block.timestamp <= deadline, "Transaction expired");
    address erc721Address = erc721Addresses[erc721Type_];  
    require(erc721Address != address(0), "INVALID_ERC721_TYPE");
    require(tokenIds_.length > 0, "INVALID_TOKEN_IDS"); 
    _processBatch(from_, erc721Address, tokenIds_.length, deadline, tokenIds_, signatures);
    emit NftImported(from_, erc721Type_, tokenIds_, id_);
  }
```

In `GuitarCryptone::_permit`

```diff
function _permit(address spender, uint256 tokenId, uint256 deadline, bytes calldata signature) internal virtual {
+   require(block.timestamp <= deadline, "Transaction expired");
    bytes32 structHash = keccak256(abi.encode(PERMIT_TYPEHASH, spender, tokenId, _nonces[tokenId], deadline));
    bytes32 hash = _hashTypedDataV4(structHash);

    address signer = ECDSA.recover(hash, signature);
    bool isValidEOASignature = signer != address(0) && _isApprovedOrOwner(signer, tokenId);
    require(signer != address(0), "ERC721Permit: Zero signer!");
    require(_isApprovedOrOwner(signer, tokenId), "ERC721Permit: not owner!");

    require(
      isValidEOASignature ||
        _isValidContractERC1271Signature(ownerOf(tokenId), hash, signature) ||
        _isValidContractERC1271Signature(getApproved(tokenId), hash, signature),
      "ERC721Permit: invalid signature"
    );

    _permitApprove(spender, tokenId);
  }
```
